apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    def acceleratorCoreVersion = '1.0.19'
    compileSdkVersion 27
    buildToolsVersion "27.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.19"
        project.archivesBaseName = "opentok-accelerator-core"
        project.version = acceleratorCoreVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            buildConfigField 'String', 'acceleratorCoreVersion', '\"'+acceleratorCoreVersion+'\"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'OldTargetApi'
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = outputFileName
            if (outputFile != null && outputFile.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                outputFileName = new File(outputFile, fileName)
            }
        }
        def javadoc = task("generateJavaDoc${variant.name.capitalize()}", type: Javadoc, dependsOn: "generate${variant.name.capitalize()}Sources") {
            source = 'src/main/java'
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = file("build/outputs/docs/")
            title = 'OpenTok Accelerator Core Android'
            //exclude "**/config/**"
            failOnError false
        }
        def zipBundle = task("zipBundle${variant.name.capitalize()}", type: Zip) {
            from ('build/outputs')
            include '**/aar/**'
            include '**/docs/**'
            exclude '**/lint-results-debug_files/**'
            destinationDir = file(".")
            archiveName = "${archivesBaseName}-${version}.zip"
        }
        task("printVersion${variant.name.capitalize()}") {
          println project.version
        }
        zipBundle.dependsOn build
        zipBundle.dependsOn javadoc
    }
}
ext.sdkVersion = 2.13
ext.previewVersion = sdkVersion+0.01

dependencies {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url 'http://tokbox.bintray.com/maven' }
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.opentok.android:opentok-solutions-logging:+'
    if (isPreview()) {
        compile "com.opentok.android:opentok-android-sdk-preview:$previewVersion.+"
    } else {
        compile "com.opentok.android:opentok-android-sdk:$sdkVersion.+"
    }
}

if (project.hasProperty("MAVEN_USERNAME") && project.hasProperty("MAVEN_PASSWORD")) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://api.bintray.com/maven/tokbox/maven/" + getArtifactId() + "/;publish=1") {
                    authentication(userName: "$MAVEN_USERNAME" ? "$MAVEN_USERNAME" : "", password: "$MAVEN_PASSWORD" ? "$MAVEN_PASSWORD" : "")
                }
                def (revision, buildTime) = getRevisionAndTime()
                pom.version = "${version}"
                pom.groupId = 'com.opentok.android'
                pom.artifactId = getArtifactId()
            }
        }
   }
}
def checkMaven = task("checkUploadArchivesDeps") {
    doFirst {
        if (!project.hasProperty("MAVEN_USERNAME")) {
            throw new GradleException("MAVEN_USERNAME Property not defined")
        }
        if (!project.hasProperty("MAVEN_PASSWORD")) {
            throw new GradleException("MAVEN_PASSWORD Property not defined")
        }
    }
}
uploadArchives.dependsOn checkMaven
def getRevisionAndTime() {
    def date = new Date()
    def hash = 'git rev-parse --verify HEAD'.execute().text.trim()
    TimeZone.setDefault(TimeZone.getTimeZone('PST'))
    def formattedDate = date.format('yyyyMMddHHmmss')
    return ["${hash}".toString(), formattedDate.toString()]
}

def isEnvVarNotEmptyOrNull(envVar) {
    def value = System.getenv(envVar)
    return value != null && !value.isEmpty()
}

def isPreview() {
    return isEnvVarNotEmptyOrNull("PREVIEW") && '1'.equals(System.getenv('PREVIEW'));
}

def getArtifactId() {
    return isPreview() ? "${PREVIEW_ARTIFACT_ID}" : "${ARTIFACT_ID}"
}